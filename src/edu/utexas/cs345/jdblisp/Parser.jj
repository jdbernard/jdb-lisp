/**
 * Grammer and parser definition for JDB-Lisp
 */
 
options {
  JDK_VERSION = "1.5";
}
PARSER_BEGIN(Parser)
package edu.utexas.cs345.jdblisp.parser;

import edu.utexas.cs345.jdblisp.*;
public class Parser {

    private static Symbol QUOTE_SYMB = new Symbol("QUOTE");
    private static Symbol FUNCTION_SYMB = new Symbol("FUNCTION");
}

PARSER_END(Parser)

SKIP : /* WHITE SPACE */
{ 	" "
|	"\r"
|	"\t"
|	"\n"
|   "\n\r"
|   < ";" (~["\n", "\r"])* ("\n"|"\r")>
}

TOKEN : /* PUNCTUATION */
{	< LPAREN: "(" >
|	< RPAREN: ")" >
|   < NIL:    (["N","n"]["I","i"]["L","l"])>
|   < QUOTE:  "'" >
|   < FUNCTION:  "#'" >
|   < KEYWORD: ":" >

}

TOKEN : /* LITERALS & SYMBOLS */
{   < NUMB: (["+", "-"])? (["0"-"9"])+ ("." (["0"-"9"])+ )? >
|   < STRG: "\"" (~["\""])* "\"" >
|	< SYMB: (["A"-"Z", "a"-"z", "_", "+", "-", "*", "/", "=", ">", "<"])+
            (["A"-"Z", "a"-"z", "0"-"9",
                "_", "+", "-", "*", "/", "=", ">", "<"])* >
}

/**
 * SExp -> Symbol | Str | Num | List 
 */
SExp sexp():
{	SExp s = null; Token t;
}
{     s = symbol() { return s; }
    | t = <STRG> { return new Str(t.image); }
    | t = <NUMB> { return new Num(t.image); }
    | t = <QUOTE> s = sexp() { return new List(new Seq(QUOTE_SYMB, s)); }
    | t = <FUNCTION> s = sexp() { return new List(new Seq(FUNCTION_SYMB, s)); }
    | s = list() { return s; }     
}

/**
 * List -> "(" Seq ")"
 */
List list(): 
{	Seq s;
}
{     <LPAREN>  s = seq() <RPAREN> { return new List(s); }    
    | <NIL>  { return new List(null); }  // allow NIL to be shorthand for ()
}

/**
 * Seq -> null | SExp Seq
 */
Seq seq():
{	Seq sq; SExp se;
}
{  [ se = sexp() sq = seq() { return new Seq(se, sq); } ]
    { return null; }
}

/**
 * Symbol -> Symbol | Keyword Symbol
 */
Symbol symbol():
{ Token t;
}
{   t = <SYMB> { return new Symbol(t.image.toUpperCase()); }
   | <KEYWORD> t = <SYMB> { return new Keyword(t.image.toUpperCase()); }
}
